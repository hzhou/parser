include: python/parse.def
include: macros_javascript/frame.def
include: macros_javascript/setup.def
include: macros_javascript/support.def
include: macros_javascript/operations.def
include: macros_javascript/statements.def

#---- main --------------
page: parse_javascript, basic_frame
    module: python

    &call quote, s
        a=1
        1+2*3;
        !a?2:3

    print parse_javascript(s)

#---- ------------------
fncode:: parse_javascript(src)
    $call parse_operator_precedence

    #-- spaces and comments -----
    subcode:: match
        $if $(type:-1) in boc_type
            $call skip_space_wide
        $else
            $call skip_space_narrow
        $if src_pos>=src_len
            continue

    #-- end of statement ----
    subcode:: match
        $if match_end_of_statement()
            cur = (';', ';')
            break

    subcode:: match
        $call @match_statements

    #-- match primary -------------------------
    subcode:: match
        #- keyword data -----------
        &call if_match_break, true|false
            cur = (m.group(0), "bool")
        &call if_match_break, null
            cur = (m.group(0), "null")

        #- string ------------
        &call if_match_break, \"(([^\\\"]|\\.)*)\"
            s=js_double_string(m.group(1))
            cur = (s, "string")
        &call if_match_break, '(([^\\']|\\.)*)'
            s=js_single_string(m.group(1))
            cur = (s, "string")
        &call if_match_break, /([^\\]|\\.)+/[igm]*
            cur = (m.group(0), "regex")

        #- numbers ------------
        &call if_match_break, r"0x[0-9a-f]+", re.I
            cur = (int(m.group(0), 16), "int")
        &call if_match_break, r"0[0-7]*", re.I
            cur = (int(m.group(0), 8), "int")
        &call if_match_break, (\d+)(\.\d+)?([eE][+-]?\d+)?
            $if m.group(2) or m.group(3)
                cur = (float(m.group(0)), "float")
            $else
                cur = (int(m.group(1)), "int")

        #-- match brackets -------------------------
        &call if_match_continue, [\[\(\{]
            op = m.group(0)
            $if $(type:-1) in atom_type
                $if op == '('
                    $call start_context, fcall
                $elif op== '['
                    $call start_context, index
                $else
                    $call error, "error opening bracket '"+op+"', forgot ';'?"
            $else
                $if op == '{'
                    $if $(type:-1)=='boc'
                        $call start_context, compound
                        cur_context["statements"]=[]
                    $else
                        $call start_context, object
                $elif op == '['
                    $call start_context, array
                $else # plain old parenthesis!
                    stack.append: ('(', '(')

        &call if_match_break, [\]\)\}]
            op = m.group(0)
            cur = (op, ')')
            #- operators need to be separated by atom
            #-   empty brackets is a problem. Hack it.
            $if $(type:-1)=='(' or $(type:-1)=="boc"
                stack.append: ('', '')

    #-- match operators -------------------------
    subcode:: match
        $if_match :
            cur = (":", ":")
            $call @match_colon_special
            break

        $if_match \+\+|--
            op = m.group(0)
            $if $(type:-1) in atom_type
                $call @reduce_postfix
            $else
                stack.append: (op, "unary")
            continue

        $if_match \.$(pat_identifier)
            op = m.group(0)
            $if $(type:-1) in atom_type
                $call @reduce_postfix
            $else
                $call error, "member not following atom"

        $call match_op, new|in|delete|typeof|void|instanceof
        $call match_op, (==?=?|!==?|>>?>?=?|<<?=?|&&?|\|\|?)
        $call match_op, [+\-*/%^&|]=?
        $call match_op, [,~!\?]

        subcode: match_op(@pat)
            $if_match $(pat)
                op = m.group(0)
                cur = (op, op)
                break

    #-- identifiers --------------
    subcode:: match
        $if_match $(pat_identifier)
            cur = (m.group(0), "identifier")
            $call skip_space_narrow
            $if_match :
                stmt = (cur[0], "label")
                got_statement(stmt)
                continue
            break


    #-- reduce -------------------------
    subcode: reduce
        # - group operators ----------
        $if $(type:-2) == 'boc'
            $call @reduce_context
            break
        $elif $(type:-2) == '('
            cur = stack[-1]
            stack[-2:]=[]
            break
        $call @reduce_other
        # - normal operators ----------
        $elif $(type:-2) == ','
            $call reduce_list
        $elif $(type:-2) == "unary"
            t = ( ($(atom:-2), stack[-1]), "unary_exp")
            stack[-2:]=[t]
        $elif $(type:-2) == ':' and len(stack)>5 and $(type:-4)=='?'
            t = ( (stack[-5], stack[-3], stack[-1]), "conditional_exp")
            stack[-5:]=[t]
        $else
            t = ( ($(type:-2), stack[-3], stack[-1]), "binary_exp")
            stack[-3:]=[t]

    #-- reduce list --------------
    subcode: reduce_list
        t=stack[-3]
        $if t[1]=="list_exp"
            t[0].append( stack[-1] )
        $else
            t = ( [stack[-3], stack[-1]], "list_exp")
        stack[-3:]=[t]

    #-- reduce postfix ------------------
    subcode: reduce_postfix
        stack[-1] = ( (op, stack[-1]), "postfix_exp")

    #-- reduce context ---------
    subcode: reduce_context
        t_op      = cur
        t_context = $(atom:-2)
        t_atom    = stack[-1]
        cur = None
        stack[-2:]=[]
        $call pop_context
        $if 0
            pass
        $call @reduce_context_cases

    subcode:: reduce_context_cases
        $elif t_context == "fcall"
            stack[-1]=( (stack[-1], t_atom), "fcall")
        $elif t_context == "index"
            stack[-1]=( (stack[-1], t_atom), "index")
        $elif t_context == "array"
            $if t_atom[1]==""
                t_atom=[]
            cur = (t_atom, "array")
        $elif t_context == "object"
            $if t_atom[1]==""
                t_atom=[]
            cur = (t_atom, "object")

    #-- reduce object_item -----
    subcode:: reduce_other
        $elif $(type:-2)==':' and cur_context["type"]=="object"
            $if $(type:-4)==',' or $(type:-4)=="boc"
                # check $(type:-3) is identifier or string
                t_name = $(atom:-3)
                t = ( (t_name, stack[-1]), "object_item")
            $else
                $call error, "object colon in the wrong place"

    #-- compound statement ----------
    subcode:: reduce_context_cases
        $elif t_context =="compound"
            last_context["statements"].append(t_atom)
            stmt = (last_context["statements"], "compound")
            got_statement(stmt)

    #-- post process -------------
    subcode: post_process
        $if $(cur_type) == ';'
            stmt = stack.pop()
            got_statement(stmt)
            continue

    fncode: got_statement(stmt)
        nonlocal src_pos, cur_context
        #- because ';' has precedence of 1
        #--  on wrapping statement, $(type:-1) has to be 'boc' or 'bof', 
        $if "statements" in cur_context
            cur_context['statements'].append(stmt)
        $else
            t_context = stack[-1][0]
            $call pop_context
            $if 0
                pass
            $call @statement_context_cases

        # print stmt

