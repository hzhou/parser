# -------------------------------------
fncode: match_Number
    $return_type double *
    tpf_ret=malloc(sizeof(double))
    $local double tf_val=0.0
    $local int tn_val=0

    $call input_get_c, tn_c
    $if !isdigit(tn_c)
	$if tn_c>0
	    $call input_back_char
	return NULL
    $else
	$if tn_c=='0'
	    tn_matched=0
	    $call input_get_c, tn_c
	    $if tn_c=='x' || tn_c=='X'
		tn_matched=1
		$call @match_hexdecimal
	    $elif tn_c=='b' || tn_c=='B'
		tn_matched=2
		$call @match_binary
	    $if tn_c>0
		$call input_back_char
	    $if tn_matched>0
		*tpf_ret=tn_val
		return tpf_ret
	$else
	    tn_val=tn_c-'0'

	tn_matched=0
	$while 1
	    $call input_get_c, tn_c
	    $if tn_c>='0' && tn_c<='9'
		tn_val=tn_val*10+(tn_c-'0')
		continue
	    $elif tn_c=='.'
		tf_val=tn_val
		$call match_float_point
		tn_matched=2
	    $elif tn_c=='e' || tn_c=='E'
		tf_val=tn_val
		$call match_float_exp
		tn_matched=2
	    $else
		tn_matched=1

	    $if tn_c>0
		$call input_back_char

	    $if tn_matched==1
		*tpf_ret=tn_val
		return tpf_ret
	    $elif tn_matched==2
		*tpf_ret=tf_val
		return tpf_ret

subcode: match_hexdecimal
    # ---- Hexdecimal 
    $while 1
	$call input_get_c, tn_c
	$if tn_c>='0' && tn_c<='9'
	    tn_val=tn_val*16+(tn_c-'0')
	$elif tn_c>='a' && tn_c<='f'
	    tn_val=tn_val*16+10+(tn_c-'a')
	$elif tn_c>='A' && tn_c<='F'
	    tn_val=tn_val*16+10+(tn_c-'A')
	$else
	    break

subcode: match_binary
    # ---- Binary 
    $while 1
	$call input_get_c, tn_c
	$if tn_c>='0' && tn_c<='1'
	    tn_val=tn_val*2+(tn_c-'0')
	$else
	    break

subcode: match_float_point
    tf_place=1
    $while 1
	tf_place*=0.1
	$call input_get_c, tn_c
	$if tn_c>='0' && tn_c<='9'
	    tf_val+=(tn_c-'0')*tf_place
	$elif tn_c=='e' || tn_c=='E'
	    $call match_float_exp
	$else
	    break

subcode: match_float_exp
    tn_power=0
    $while 1
	$call input_get_c, tn_c
	$if tn_c>='0' && tn_c<='9'
	    tn_power=tn_power*10+(tn_c-'0')
	$else
	    tf_val*=pow(10, tn_power)
	    break

