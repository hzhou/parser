include: python/parse.def
include: macros_calc/setup.def

#---- main --------------
page: calc, basic_frame
    module: python
    $(if:0)
        print calc("-1+2*-3")
    $(else)
        $call repl

#---- repl loop ---------
subcode: repl
    $import readline
    $while 1
        line = input("> ")
        $if line=="quit"
            break
        $else
            print calc(line)

#---- ------------------
fncode: calc(src)
    $call parse_operator_precedence

    #-- match -------------------------
    subcode: match
        $if_match \s+
            continue

        $if_match [\d\.]+
            num = float(m.group(0))
            cur=( num, "num")
            break

        $if_match (
            cur = ('(', '(')
            break
        $if_match )
            cur = (')', ')')
            break

        $if_match \+\+|--
            op = m.group(0)
            $if $(type:-1) in atom_type
                $call @reduce_postfix
            $else
                stack.append: (op, "unary")
            continue

        #- operators -------------
        $call match_op, new|in|delete|typeof|void|instanceof
        $call match_op, (==?=?|!==?|>>?>?=?|<<?=?|&&?|\|\|?)
        $call match_op, [+\-*/%^&|]=?
        $call match_op, [,~!\?:]

        subcode: match_op(@pat)
            $if_match $(pat)
                op = m.group(0)
                cur = (op, op)
                break

        subcode: reduce_postfix
            pass

    #-- reduce -------------------------
    subcode: reduce
        $if $(type:-2) == '('
            cur = stack[-1]
            stack[-2:]=[]
        $elif $(type:-2) == "unary"
            $if $(atom:-2)=='-'
                t = -$(atom:-1)
            $elif $(atom:-2)=='!'
                t =  not $(atom:-1)
            $elif $(atom:-2)=='~'
                t = ~int($(atom:-1))
            $else
                $call error, "unhandled unary operator ["+$(atom:-2)+"]"
            stack[-2:]=[(t, "num")]
        $map reduce_binary, +, -, *, /
        $map reduce_binary_int, %, <<, >>, ^, &, |
        $call reduce_binary_2, &&, and
        $call reduce_binary_2, ||, or
        $call @reduce_other
        $else
            $call error, "unhandled operator ["+$(type:-2)+"]"

        subcode: reduce_binary(op)
            $elif $(type:-2)=='$(op)'
                t = $(atom:-3) $(op) $(atom:-1)
                stack[-3:]=[(t, "num")]

        subcode: reduce_binary_int(op)
            $elif $(type:-2)=='$(op)'
                t = int($(atom:-3)) $(op) int($(atom:-1))
                stack[-3:]=[(t, "num")]
            
        subcode: reduce_binary_2(op, op2)
            $elif $(type:-2)=='$(op)'
                t = $(atom:-3) $(op2) $(atom:-1)
                stack[-3:]=[(t, "num")]
            
    #-- reduce_conditional ------------
    subcode:: reduce_other
        $call reduce_conditional

        subcode: reduce_conditional
            $elif $(type:-2) == ':'
                $if len(stack)>5 and $(type:-4)=='?'
                    $if $(atom:-5)
                        stack[-5:]=[stack[-3]]
                    $else
                        stack[-5:]=[stack[-1]]
                $else
                    $call error, "Ternary operators have been messed up."

